group 'com.novarto'
version '0.9-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://oss.jfrog.org/artifactory/libs-snapshot/'
        }
    }
    dependencies {
        classpath group: 'com.novarto', name: 'java-conventions',
                version: '0.9.1-SNAPSHOT'
    }
}

subprojects {

    repositories {
        maven {
            url 'https://oss.jfrog.org/artifactory/libs-snapshot/'
        }
    }

    group project.rootProject.group
    version project.rootProject.version

    apply plugin: 'com.novarto.javaconventions'
    apply plugin: 'com.novarto.osspublish'



    javaconventions {
        ci = System.getProperty("ci")!=null || ("true".equals(System.getenv("TRAVIS")) && "true".equals(System.getenv("CI")))
    }

    dependencies {
        testCompile 'org.hamcrest:hamcrest-all:1.3'
        testCompile 'junit:junit:4.12'
        testCompile "org.functionaljava:functionaljava-quickcheck:4.7"
        testCompile 'org.hsqldb:hsqldb:2.3.4'
    }

    task maybePublish {

        def isTravis = "true".equals(System.getenv("TRAVIS"))
        def isSnapshot = version.toString().endsWith("SNAPSHOT")
        def isNotPr = "false".equals(System.getenv("TRAVIS_PULL_REQUEST"))
        def isMaster = "master".equals(System.getenv("TRAVIS_BRANCH"))

        if(isTravis && isSnapshot && isNotPr && isMaster) {
            println("Travis snapshot master non-pr build detected, will publish to artifactory")
            finalizedBy(artifactoryPublish)
        }

        else {
            doLast {
                println("Not a Travis snapshot master non-pr build, skipping publish to artifactory")
            }
        }
    }

}



